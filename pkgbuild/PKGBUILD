# Maintainer: Morgan <morganamilo@archlinux.org>
pkgname=paru-git
_pkgname=paru
pkgver=0.0.1.r21.fd4ebc1
pkgrel=1
pkgdesc='AUR helper based on yay'
url='https://github.com/morganamilo/paru'
source=("git+https://github.com/morganamilo/paru")
arch=('x86_64' 'i686')
license=('GPL3')

makedepends=('cargo')
depends=('git' 'pacman' 'vifm')

options+=(!strip)

sha256sums=(SKIP)

build () {
  cd "$srcdir/$_pkgname"

  if pacman -T pacman-git > /dev/null; then
    _features+="git,generate,"
  fi

  if [[ $(rustc -V) == *"nightly"* ]]; then
    _features+="backtrace,"
  fi

  PARU_VERSION=$pkgver cargo build --features "${_features:-}" --release
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 target/release/paru "${pkgdir}/usr/bin/paru"
  install -Dm644 paru.conf "${pkgdir}/etc/paru.conf"
}

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "0.0.1.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

